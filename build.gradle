plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '2.6.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

def elasticVersion = '6.8.20'

group = 'uk.gov.digital.ho.hocs'
sourceCompatibility = '11'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

plugins.withType(JacocoPlugin) {
    tasks["test"].finalizedBy 'jacocoTestReport'
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
        html.required = false
        csv.required = false
        //for XML
        xml.destination file("${buildDir}/reports/jacoco.xml")
    }
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude(module: 'spring-boot-starter-tomcat')
    }

    implementation 'org.springframework.boot:spring-boot-starter-undertow'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.cloud:spring-cloud-aws-messaging:2.2.6.RELEASE'


    implementation 'net.logstash.logback:logstash-logback-encoder:7.0.1'

    // Override the default version of elasticSearch, we're still using V6
    implementation "org.elasticsearch:elasticsearch:${elasticVersion}"
    implementation "org.elasticsearch.client:elasticsearch-rest-client:${elasticVersion}"
    implementation "org.elasticsearch:elasticsearch-core:${elasticVersion}"
    implementation "org.elasticsearch:elasticsearch-secure-sm:${elasticVersion}"

    // Cannot update yet due to running old instances in environments
    implementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client:6.6.2'

    implementation 'com.github.awslabs:aws-request-signing-apache-interceptor:b3772780da'

    compileOnly 'org.projectlombok:lombok:1.18.22'

    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.springframework:spring-context-indexer:5.3.17'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.awaitility:awaitility:4.2.0'
    testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:2.32.0'

}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2021.0.1"
    }
}

jar {
    enabled = false
}

test {
    useJUnitPlatform()
}
