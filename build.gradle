buildscript {
	ext {
		springBootVersion = '2.1.1.RELEASE'
		camelVersion = '2.23.0'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'

task copyLombok(type: Copy) {
	outputs.upToDateWhen { false }
	into "${buildDir}/libs"
	include "lombok*.jar"
	from configurations.compileOnly
}

tasks.withType(JavaCompile) {

    options.fork = true  // Fork your compilation into a child process
    options.forkOptions.setMemoryMaximumSize("256m")
}

test {
    maxHeapSize = "256m"
}

build.dependsOn(copyLombok)

group = 'uk.gov.digital.ho.hocs'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
	jcenter()
    maven { url 'https://jitpack.io' }

}

dependencies {

    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude(module: 'spring-boot-starter-tomcat')
    }
    implementation('org.springframework.boot:spring-boot-starter-undertow')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    
    implementation('org.elasticsearch.client:elasticsearch-rest-high-level-client:6.6.2')

    implementation('org.springframework.boot:spring-boot-starter-json')
    implementation('org.springframework.boot:spring-boot-starter-cache')
    implementation('org.springframework.retry:spring-retry')
    implementation('org.springframework:spring-aspects')
    implementation("org.apache.camel:camel-spring-boot-starter:${camelVersion}")

    implementation("org.apache.camel:camel-jackson:${camelVersion}")
    implementation("org.apache.camel:camel-aws:${camelVersion}")
    implementation("org.apache.camel:camel-http4:${camelVersion}")

    implementation('org.apache.httpcomponents:httpmime:4.5.6')
    implementation('com.amazonaws:aws-java-sdk:1.11.470')
    implementation 'com.github.awslabs:aws-request-signing-apache-interceptor:b3772780da'

    implementation('net.logstash.logback:logstash-logback-encoder:5.2')

    compileOnly('org.projectlombok:lombok')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation("org.apache.camel:camel-test-spring:${camelVersion}")
    testImplementation('org.assertj:assertj-core')
}